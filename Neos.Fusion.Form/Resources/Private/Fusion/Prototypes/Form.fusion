prototype(Neos.Fusion.Form:Form) < prototype(Neos.Fusion:Component) {
    @context.form = ${this.form}
    form = Neos.Fusion.Form:FormDefinition
    attributes = Neos.Fusion:DataStructure

    targetUri = Neos.Fusion:UriBuilder
    method = 'post'
    enctype = null

    renderer = afx`
        <form
            {...props.attributes}
            action={props.targetUri}
            method={props.method}
            enctype={props.enctype}
        >
            <div style="display: none">

                <Neos.Fusion.Form:Fragment @if.isNotMainRequest={!request.mainRequest} >
                    <input type="hidden" name={request.argumentNamespace + "[__referrer][@package]"} value={request.parentRequest.controllerPackageKey} />
                    <input type="hidden" name={request.argumentNamespace + "[__referrer][@subpackage]"} value={request.parentRequest.controllerSubpackageKey} />
                    <input type="hidden" name={request.argumentNamespace + "[__referrer][@controller]"} value={request.parentRequest.controllerName} />
                    <input type="hidden" name={request.argumentNamespace + "[__referrer][@action]"} value={request.parentRequest.controllerActionName} />
                    <input type="hidden" name={request.argumentNamespace + "[__referrer][arguments]"} value={Form.argumentsWithHmac(request.parentRequest.arguments)} />
                </Neos.Fusion.Form:Fragment>

                <Neos.Fusion.Form:Fragment>
                    <input type="hidden" name="__referrer[@package]" value={request.controllerPackageKey} />
                    <input type="hidden" name="__referrer[@subpackage]" value={request.controllerSubpackageKey} />
                    <input type="hidden" name="__referrer[@controller]" value={request.controllerName} />
                    <input type="hidden" name="__referrer[@action]" value={request.controllerActionName} />
                    <input type="hidden" name="__referrer[arguments]" value={Form.argumentsWithHmac(request.arguments, request.argumentNamespace)} />
                </Neos.Fusion.Form:Fragment>

                <input
                    type="hidden"
                    name={Form.prefixFieldName('__trustedProperties', form.fieldNamePrefix)}
                    value={Form.trustedPropertiesToken(props.content, form.fieldNamePrefix)}
                    />

                <input type="hidden" name="__csrfToken" value={Form.csrfToken()} />
            </div>
            {props.content}
        </form>
    `
}

// this should go to fusion itself on the long run
prototype(Neos.Fusion.Form:Fragment) < prototype(Neos.Fusion:Component) {
  renderer = ${props.content}
}
